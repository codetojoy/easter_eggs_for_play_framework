
plugins {
    id("java")
    id("io.ebean") version "15.3.0"
    id("application")
}

project.ext.ebeanVersion = "15.3.0"

project.ext.pgVersion = "42.7.1"
project.ext.hikariVersion = "5.1.0"
project.ext.logbackVersion = "1.5.3"
project.ext.slf4jVersion = "2.0.12"
project.ext.avajeLogbackVersion = "1.1"

project.ext.distDir = "dist"
project.ext.distZip = "${projectDir}/../play_webapp/target/universal/play-as-sdk-example-0.9.0.zip"
project.ext.distJars = "dist/play-as-sdk-example-0.9.0/lib"

application {
    mainClass = 'app.App'
}

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor "io.ebean:querybean-generator:${ebeanVersion}"

    implementation "io.ebean:ebean:${ebeanVersion}"
    implementation "io.ebean:ebean-querybean:${ebeanVersion}"

    implementation "org.postgresql:postgresql:${pgVersion}"
    implementation "com.zaxxer:HikariCP:${hikariVersion}"

    implementation "org.avaje.composite:logback:${avajeLogbackVersion}"
    implementation "ch.qos.logback:logback-classic:${logbackVersion}"
    implementation "ch.qos.logback:logback-core:${logbackVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "org.slf4j:jcl-over-slf4j:${slf4jVersion}"

    implementation fileTree(dir: distJars, includes: ['*.jar'])
}

task unpackDistribution(type: Copy) {
    from(zipTree(distZip)) {
        // exclude '**/*ebean*.jar'
    }
    into distDir
}

compileJava.dependsOn 'unpackDistribution'

ebean {
    debugLevel = 0 
}

test {
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
}

